Program.Sub.ScreenSU.Start
Gui.F_Contacts..create
Gui.F_Contacts..caption("Form1")
Gui.F_Contacts..size(5970,3180)
Gui.F_Contacts..position(0,0)
Gui.F_Contacts..event(UnLoad,f_contacts_unload)
Gui.F_Contacts..forecolor(0)
Gui.F_Contacts..fontstyle(,)
Gui.F_Contacts..BackColor(-2147483633)
Gui.F_Contacts..maxbutton(False)
Gui.F_Contacts..mousepointer(0)
Gui.F_Contacts..sizeable(False)
Gui.F_Contacts..MinX(0)
Gui.F_Contacts..MinY(0)
Gui.F_Contacts.lbl1.create(label,"Please enter e-Invoicing contacts (e.g., emailaddress1@companyname.com, emailaddress2@companyname.com, etc.)",True,6030,390,1,200,200,True,0,Arial,8,-2147483633,0)
Gui.F_Contacts.txtContacts.create(textboxm)
Gui.F_Contacts.txtContacts.size(5550,1500)
Gui.F_Contacts.txtContacts.position(200,700)
Gui.F_Contacts.cmdSave.create(button)
Gui.F_Contacts.cmdSave.caption("Save")
Gui.F_Contacts.cmdSave.size(855,375)
Gui.F_Contacts.cmdSave.position(200,2300)
Gui.F_Contacts.cmdSave.event(Click,cmdsave_click)
Gui.F_EmailResult..Create
Gui.F_EmailResult..Caption("Email Result")
Gui.F_EmailResult..Size(16755,9285)
Gui.F_EmailResult..MinX(0)
Gui.F_EmailResult..MinY(0)
Gui.F_EmailResult..Position(0,0)
Gui.F_EmailResult..BackColor(-2147483633)
Gui.F_EmailResult..MousePointer(0)
Gui.F_EmailResult..Event(UnLoad,F_EmailResult_UnLoad)
Gui.F_EmailResult.txtEmailResult.Create(TextBoxR)
Gui.F_EmailResult.txtEmailResult.Size(15990,8400)
Gui.F_EmailResult.txtEmailResult.Position(300,240)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
v.Global..BulkDeclareString(sCustomer, sInvoiceNo, sOrderNo, sCustPO, sCompany, sCustomerName)
v.Global..BulkDeclareLong(iLogId, iBIRUNID)
Variable.Global.bParamCheck.Declare(Boolean,False)
v.Global.bNoInvoice.Declare(boolean)
v.Global.sResult.Declare
Program.Sub.Preflight.End

Program.Sub.Main.Start
'BN Feb 2, 2017
'Customer: Conveyors, Inc.
'Project Contact: Teresa Gomez
'4262 is a variation of ATG E-Invoice (old version)
'Based on the value in Customer Master -> Options -> Invoice/Credit Memo box.
'If that value is: 
'000055 => use report OE_INVOICE_NoBuyGroupRev.RPT, 
'100003 => use report OE_INVOICE_AddBuyGroupcopyRev.RPT 

'BN, June 8, 2018
'The program will check if a customer is a member of a BUYING GROUP
'If in BUYING GROUP, will ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

'The customer selected 
	'Contact Method A (Customer Master) and does not use method via CRM
	'Delivery Method A (Both Print and Email)
'Script 1 button will be set as "E-Invoice" for Audit
'Script 2 button will be set as "e-Invoice Cont"

'ATG_INVOICE_DELIVERY.gas is modified to be GCG_4262_INVOICE_DELIVERY.gas. It will set Contacts for script 2 button ("e-Invoice Cont" button) (hook 14370), and used to automatically email when Update Invoice Batches is hit (hook 15225)
'ATG_AUTO_EMAIL_CUST.gas is modified to be GCG_4262_AUDIT_CUST.gas. It will used for the script 1 button ("E-Invoicing" button)(hook 14360) for the AUDIT.

'Tables used: 
'ATG_CUSTOMER_EMAIL,ATG_CUST_EMAIL_HIST,
'SALESPEOPLE,CUSTOMER_MASTER,CONTACT,Cust_Form_Info (to get value from Invoice/Credit Memo),ORDER_HIST_HEAD
'No need for table : 'ATG_EINVOICE_CCC because they only have a single live company code.

F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors
V.Local.sError.Declare

F.ODBC.Connection!conx.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.Control.If(V.Caller.Hook,=,14370)
	'Script 2 is clicked
	F.Intrinsic.Control.CallSub(Contacts)
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,15225)
	V.Global.sCustomer.Set(V.Passed.009001)
	V.global.sInvoiceNo.Set(V.Passed.009000)		
	F.Intrinsic.Control.CallSub(Popdefaulttext)
	F.Intrinsic.Control.CallSub(einvoicing)	
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4262_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub(unload)
Function.Intrinsic.Control.EndIf
Program.Sub.Main.End

Program.Sub.Contacts.Start
F.Intrinsic.Control.SetErrorHandler("Contacts_Err")
F.Intrinsic.Control.ClearErrors
v.Local..BulkDeclareString(sError,sQuery,sEmails)

F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Passed.000002,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstContacts",V.Local.sQuery)
F.Intrinsic.Control.DoUntil(V.ODBC.conx!rstContacts.EOF,=,True)
	F.Intrinsic.Control.If(V.Local.sEmails,=,"")
		V.Local.sEmails.Set(V.ODBC.conx!rstContacts.FieldValTrim!CUST_EMAIL)
	F.Intrinsic.Control.Else
		F.Intrinsic.String.Concat(V.Local.sEmails,", ",V.ODBC.conx!rstContacts.FieldValTrim!CUST_EMAIL,V.Local.sEmails)
	F.Intrinsic.Control.EndIf
	F.ODBC.conx!rstContacts.MoveNext
F.Intrinsic.Control.Loop
F.ODBC.conx!rstContacts.Close

Gui.F_Contacts.txtContacts.Text(V.Local.sEmails)

F.Intrinsic.String.Concat("e-Invoicing contacts for ",V.Passed.000005,V.Local.sQuery)
Gui.F_Contacts..Caption(V.Local.sQuery)

Gui.F_Contacts..Show

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Contacts_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub(unload)
Function.Intrinsic.Control.EndIf
Program.Sub.Contacts.End

Program.Sub.eInvoicing.Start
F.Intrinsic.Control.SetErrorHandler("eInvoicing_Err")
F.Intrinsic.Control.ClearErrors
v.Local..BulkDeclareString(sError,sQuery,sContacts,sDelete,sEmail,sTemp,sUserName,sUserEmail,sInvoicePath,sBody,sSubject,sFile,sMsg)
V.Local.sContacts.Redim(-1,-1)
V.Local.sDelete.Redim(-1,-1)
v.Local..BulkDeclareLong(iC, iType, iRet)
V.Local.iUB.Declare(Long,-1)
V.Local.iRecord.Declare(Long,-1)
V.Local.iEmailSalesRep.Declare(Long,-1)
v.Local..BulkDeclareBoolean(bFileExists, bExists)

'Emailing Invoice No - V.Global.sInvoiceNo to customer 
F.Intrinsic.Control.CallSub(Check_econtacts_meth)
V.Local.iType.Set(V.Args.iType)

'Gets all Customer Contacts and builds into sContacts array
F.Intrinsic.Control.If(V.Local.iType,=,0)
	F.Intrinsic.String.Concat("SELECT EMAIL1, NAME FROM CONTACT WHERE TYPE='C' AND CUST='",V.Global.sCustomer,"'",V.Local.sQuery)
	F.ODBC.Connection!conx.OpenRecordsetRO("rstCust",V.Local.sQuery)
	F.Intrinsic.Control.DoUntil(V.ODBC.conx!rstCust.EOF,=,True)
		F.Intrinsic.Math.Add(V.Local.iUB,1,V.Local.iUB)
		F.Intrinsic.Control.If(V.Local.iUB,=,0)
			V.Local.sContacts.Redim(0,0)
		F.Intrinsic.Control.Else
			V.Local.sContacts.RedimPreserve(0,V.Local.iUB)
		F.Intrinsic.Control.EndIf
		F.Intrinsic.String.Concat(V.ODBC.conx!rstCust.FieldValTrim!EMAIL1,"*!*",V.ODBC.conx!rstCust.FieldValTrim!NAME,V.Local.stemp)
		V.Local.sContacts(v.Local.iUB).Set(V.Local.sTemp)
		F.ODBC.conx!rstCust.MoveNext
	F.Intrinsic.Control.Loop
	F.ODBC.conx!rstCust.Close
F.Intrinsic.Control.EndIf
V.Local.iUB.Set(-1)

'===========================================================================================================================================================================
'BN June 7, 2017 check if member of buying group?
'If in buying group, look for member email address, if there isn't any email address, look for the parent email address
v.Local.iEmailExists.Declare(long)
v.Local.sHold.Declare(string)
'Flag used in case when a customer belongs to a Buying Group and the customer does not have any email address setup but the Buying Group may have an email address setup
v.Local.bFlag.Declare(boolean)
F.Intrinsic.String.Concat("SELECT count(*) FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Global.sCustomer,"'",V.Local.sQuery)
f.ODBC.Connection!conx.ExecuteAndReturn(v.Local.sQuery,v.Local.iEmailExists)
f.Intrinsic.Control.If(v.Local.iEmailExists,=,0)
	'1/Customer does not has an email address	
	'Check to see if this customer belongs to a Buying Group
	F.Intrinsic.String.Build("Select Top 1 Group_Cust from Buying_group where Customer = '{0}'",v.Global.sCustomer,v.Local.sQuery)
	f.ODBC.Connection!conx.ExecuteAndReturn(v.Local.sQuery,v.Local.sHold)
	f.Intrinsic.Control.If(v.Local.sHold,<>,"")
		'This customer belong to a Buying Group
		'Get the emails from the Buying Group
		F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",v.Local.sHold,"'",V.Local.sQuery)		
	f.Intrinsic.Control.Else
		'This customer does not has any Buying Group
		'Still use this Query but it will not return anything and the following will not process (send email)
		F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Global.sCustomer,"'",V.Local.sQuery)
	f.Intrinsic.Control.EndIf
f.Intrinsic.Control.Else
	'2/Customer has email Address =>  use that email
	F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Global.sCustomer,"'",V.Local.sQuery)
f.Intrinsic.Control.EndIf

'===========================================================================================================================================================================
F.ODBC.connection!conx.OpenRecordsetRW("rstCust",V.Local.sQuery)
F.Intrinsic.Control.DoUntil(V.ODBC.conx!rstCust.EOF,=,True)
	F.Intrinsic.String.occurs(V.ODBC.conx!rstCust.FieldValTrim!Cust_Email,"@",V.Local.iRet)
	F.Intrinsic.Control.If(V.Local.iRet,>,0)
		'If this is the first loop, iEmailSalesRep=-1.  After that, iEmailSalesRep will be set as either 0 or 1, depending on the option set for the customer.  So in the first loop, where iEmailSalesRep=1, is where we process the invoice, so it only happens once.
		F.Intrinsic.Control.If(V.Local.iEmailSalesRep,=,-1)
			'Generates Invoice PDF
			F.Intrinsic.String.Concat("SELECT ORDER_NO FROM ORDER_HIST_HEAD WHERE INVOICE='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
			F.ODBC.Connection!conx.OpenRecordsetRO("rstOrder",V.Local.sQuery)
			F.Intrinsic.Control.If(V.ODBC.conx!rstOrder.EOF,<>,True)
				V.Global.sOrderNo.Set(V.ODBC.conx!rstOrder.FieldValTrim!ORDER_NO)
				'Gets e-Invoicing Body Text
				F.Intrinsic.Control.CallSub(Popdefaulttext)
				V.Local.sBody.Set(V.Args.sBody)
				V.Local.sSubject.Set(V.Args.sSubject)
				F.Intrinsic.Control.CallSub("ProcessInv")
				V.Local.sInvoicePath.Set(V.Args.sAttach)
			F.Intrinsic.Control.Else
				F.Intrinsic.UI.Msgbox("Unable to find order number. Please contact Global Shop","e-Invoicing")
				F.Intrinsic.Control.CallSub(Unload)
			F.Intrinsic.Control.EndIf
			F.ODBC.conx!rstOrder.Close

			F.Intrinsic.Control.If(V.ODBC.conx!rstCust.fieldvaltrim!SALES_EMAIL,=,True)
				V.Local.iEmailSalesRep.Set(1)
				F.Intrinsic.String.Concat("SELECT S.SALESPERSON AS EMAIL, N.SALESPERSON FROM SALESPEOPLE S, CUSTOMER_MASTER C, V_SALESPEOPLE N WHERE N.SALESPERSON_CODE=S.SALESPERSON_CODE AND C.SALESPERSON=S.SALESPERSON_CODE AND C.CUSTOMER='",V.Global.sCustomer,"' AND S.KEY1='AR2' AND S.KEY2='SLM'",V.Local.sQuery)
				F.ODBC.Connection!conx.OpenRecordsetRO("rstSalesperson",V.Local.sQuery)
				F.Intrinsic.Control.If(V.ODBC.conx!rstSalesperson.EOF,<>,True)
					V.Local.sTemp.Redim(0,1)
					V.Local.sTemp(0).Set(V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON)
					V.Local.sTemp(1).Set(V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL)
					'email
					F.Global.Security.GetFullName(V.Caller.User,V.Local.sUserName)
					F.Global.Security.GetUserEmail(V.Caller.User,V.Local.sUserEmail)
					F.Intrinsic.Control.If(V.Local.sUserEmail,=,"")
						V.Local.sUserName.Set(V.Local.sTemp(1))
						V.Local.sUserEmail.Set(V.Local.sTemp(0))
					F.Intrinsic.Control.EndIf
					
					F.Intrinsic.Control.If(V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,<>,"")
						'BN June 12 2018 '1'	
						F.Global.Messaging.CreateEMMessage(V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,V.ODBC.conx!rstSalesperson.FieldValTrim!SALESPERSON,V.Local.sUserEmail,V.Local.sUserName,V.Local.sSubject,V.Local.sBody,V.Args.sAttach,False)
						f.Intrinsic.String.Build("{0}Email to {1} , {2} , Attach: {3}{4}",v.Global.sResult,V.ODBC.conx!rstSalesperson.FieldValTrim!EMAIL,V.Local.sSubject,V.Args.sAttach,v.Ambient.NewLine,v.Global.sResult)
					F.Intrinsic.Control.EndIf
				F.Intrinsic.Control.EndIf
				F.ODBC.conx!rstSalesperson.Close
			F.Intrinsic.Control.Else
				V.Local.iEmailSalesRep.Set(0)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf

		'Checks to see if the contact e-mail written in ATG_CUSTOMER_EMAIL is still a contact e-mail for the customer in CRM.  
		'Otherwise, it will not send an e-mail out to that address, and address to the "Delete" array,
		' which is purged from ATG_CUSTOMER_EMAIL at the end of the subroutine

		 'iType=0 if added from ATG_AUTO_EMAIL_CUST.gas from CRM, iType=1 if using script button from Customer Master (ATG_Invoice_Delivery.gas)
		F.intrinsic.control.if(V.local.iType,=,0)
		' from CRM
			V.Local.bExists.Set(False)
			F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sContacts.UBound,1)
				F.Intrinsic.String.Split(V.Local.sContacts(v.Local.iC).Trim,"*!*",V.Local.sTemp)
				F.Intrinsic.Control.If(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL,=,V.Local.sTemp(0))
					V.Local.bExists.Set(True)
					F.Intrinsic.Control.ExitFor(V.Local.iC)
				F.Intrinsic.Control.EndIf
			F.Intrinsic.Control.Next(V.Local.iC)
		F.Intrinsic.Control.Else
			V.Local.bExists.Set(True)
			V.Local.sTemp.Redim(0,1)
			V.Local.sTemp(0).Set(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL)
			V.Local.sTemp(1).Set(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL)
		F.Intrinsic.Control.EndIf

		F.Intrinsic.Control.If(V.Local.bExists,=,True)
			'email
			F.Global.Security.GetFullName(V.Caller.User,V.Local.sUserName)
			F.Global.Security.GetUserEmail(V.Caller.User,V.Local.sUserEmail)	
			F.Intrinsic.Control.If(V.Local.sUserEmail,=,"")
				V.Local.sUserName.Set(V.Local.sTemp(1))
				V.Local.sUserEmail.Set(V.Local.sTemp(0))
			F.Intrinsic.Control.EndIf

			F.Global.Messaging.CreateEMMessage(V.Local.sTemp(0),V.Local.sTemp(1),V.Local.sUserEmail,V.Local.sUserName,V.Local.sSubject,V.Local.sBody,V.Args.sAttach,False)			
			'BN June 12 2018 '2'			
			f.Intrinsic.String.Build("{0}Email to {1} , {2} , Attach: {3}{4}",v.Global.sResult,V.Local.sTemp(0),V.Local.sSubject,V.Args.sAttach,v.Ambient.NewLine,v.Global.sResult)
			
			'write record
			F.Intrinsic.Control.If(V.Local.iRecord,=,-1)
				F.ODBC.Connection!conx.OpenRecordsetRW("rstRecord","SELECT * FROM ATG_CUST_EMAIL_HIST")
				F.ODBC.conx!rstRecord.AddNew
				F.ODBC.conx!rstRecord.Set!CUSTOMER(V.Global.sCustomer)
				F.ODBC.conx!rstRecord.Set!INVOICE_NO(V.global.sInvoiceNo)
				F.ODBC.conx!rstRecord.Set!EMAIL_DATE(V.Ambient.Now)
				F.ODBC.conx!rstRecord.Update
				F.ODBC.conx!rstRecord.Close
				V.Local.iRecord.Set(0)
			F.Intrinsic.Control.Endif
		F.Intrinsic.Control.Else
			F.Intrinsic.Math.Add(V.Local.iUB,1,V.Local.iUB)
			F.Intrinsic.Control.If(V.Local.iUB,=,0)
				V.Local.sDelete.Redim(0,0)
			F.Intrinsic.Control.Else
				V.Local.sDelete.RedimPreserve(0,V.Local.iUB)
			F.Intrinsic.Control.EndIf
			V.Local.sDelete(v.Local.iUB).Set(V.ODBC.conx!rstCust.FieldValTrim!CUST_EMAIL)
		F.Intrinsic.Control.EndIf

	'EndIf iRet > 0
	F.Intrinsic.Control.EndIf
	F.ODBC.conx!rstCust.MoveNext
F.Intrinsic.Control.Loop
F.ODBC.conx!rstCust.Close

'BN June 12 2018, '3'
gui.F_EmailResult.txtEmailResult.Text(V.Global.sResult)
gui.F_EmailResult..Show
f.Intrinsic.UI.Msgbox(v.Global.sResult)

F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sDelete.UBound,1)
	F.Intrinsic.String.Concat("DELETE FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Global.sCustomer,"' AND CUST_EMAIL='",V.Local.sDelete(v.Local.iC),"'",V.Local.sQuery)
	F.ODBC.connection!conx.Execute(V.Local.sQuery)
F.Intrinsic.Control.Next(V.Local.iC)

F.Intrinsic.Control.CallSub("Unload")


F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("eInvoicing_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.eInvoicing.End

Program.Sub.f_contacts_unload.Start
F.Intrinsic.Control.SetErrorHandler("f_contacts_unload_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
F.Intrinsic.Control.CallSub(Unload)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("f_contacts_unload_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4262_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf
Program.Sub.f_contacts_unload.End

Program.Sub.cmdsave_click.Start
F.Intrinsic.Control.SetErrorHandler("cmdsave_click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sQuery.Declare(String)
V.Local.sEmails.Declare(String)
V.Local.iC.Declare(Long)

Gui.F_Contacts..Enabled(False)
F.Intrinsic.Control.CallSub(Set_econtacts_meth)

F.Intrinsic.String.Concat("DELETE FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Passed.000002,"'",V.Local.sQuery)
F.ODBC.Connection!conx.Execute(V.Local.sQuery)

F.Intrinsic.String.Replace(V.Screen.F_Contacts!txtContacts.Text,";",",",V.Local.sEmails)

F.Intrinsic.String.Split(V.local.sEmails,",",V.Local.sEmails)
F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sEmails.UBound,1)
	V.Local.sEmails(v.Local.iC).Set(V.Local.sEmails(v.Local.iC).Trim)
F.Intrinsic.Control.Next(V.Local.iC)
F.Intrinsic.String.RemoveArrayDuplicates(V.Local.sEmails,V.Local.sEmails)

F.Intrinsic.String.Concat("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='",V.Passed.000002,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRW("rstContacts",V.Local.sQuery)
F.Intrinsic.Control.For(V.Local.iC,0,V.Local.sEmails.UBound,1)
	F.ODBC.conx!rstContacts.AddNew
	F.ODBC.conx!rstContacts.Set!CUSTOMER(V.Passed.000002)
	F.ODBC.conx!rstContacts.Set!CUST_EMAIL(V.Local.sEmails(v.Local.iC))
	F.ODBC.conx!rstContacts.Set!SALES_EMAIL(False)
	F.ODBC.conx!rstContacts.Update
F.Intrinsic.Control.Next(V.Local.iC)
F.ODBC.conx!rstContacts.Close

F.Intrinsic.Control.CallSub(Unload)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("cmdsave_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.cmdsave_click.End

Program.Sub.Check_eContacts_Meth.Start
F.Intrinsic.Control.SetErrorHandler("Check_eContacts_Meth_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'Checks to see how e-Invoicing contacts were added.  iType=0 if added from ATG_AUTO_EMAIL_CUST.gas from CRM, iType=1 if using script button from Customer Master (ATG_Invoice_Delivery.gas)
V.Local.sQuery.Declare(String)
V.Local.iType.Declare(Long)

V.Local.sQuery.Set("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='-1'")
F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
	V.Local.iType.Set(0)
F.Intrinsic.Control.Else
	V.Local.iType.Set(1)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCheck.Close

F.Intrinsic.Variable.AddRV("iType",V.Local.iType)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Check_eContacts_Meth_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.Check_eContacts_Meth.End

Program.Sub.Unload.Start
F.Intrinsic.Control.SetErrorHandler("Unload_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)

F.ODBC.Connection!conx.Close
F.Intrinsic.Control.End

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Unload_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4262_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.Unload.End

Program.Sub.Set_eContacts_Meth.Start
F.Intrinsic.Control.SetErrorHandler("Set_eContacts_Meth_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'Sets e-Invoicing contacts method as iType=1 (See Check_eContacts_Meth sub).  iType=0 if added from ATG_AUTO_EMAIL_CUST.gas from CRM, iType=1 if using script button from Customer Master (ATG_Invoice_Delivery.gas)
V.Local.sQuery.Declare(String)

V.Local.sQuery.Set("SELECT * FROM ATG_CUSTOMER_EMAIL WHERE CUSTOMER='-1'")
F.ODBC.Connection!conx.OpenRecordsetRW("rstCheck",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
	F.ODBC.conx!rstCheck.AddNew
	F.ODBC.conx!rstCheck.Set!Customer("-1")
	F.ODBC.conx!rstCheck.Set!Cust_email("-1")
	F.ODBC.conx!rstCheck.Set!Sales_email(False)
	F.ODBC.conx!rstCheck.Update
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCheck.Close

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Set_eContacts_Meth_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.Set_eContacts_Meth.End

Program.Sub.ProcessInv.Start
F.Intrinsic.Control.SetErrorHandler("ProcessInv_Err")
F.Intrinsic.Control.ClearErrors

v.Local..BulkDeclareString(sError, sParamName, sParamVal, sTemp, sPN, sPV, sCallParams, sAttach, sVersion, sPath, sQuery, sReport, sSQL, sInvoiceMemoCredit, sReport_Name, sReportID)
V.Local.iRptID.Declare(Long)
v.Local..BulkDeclareBoolean(bExists, bReportFound, bFile)
'create a variable to store our required params. TERMINAL*!*REPORTID*!*Invoice
'create variables for our new stuff
V.Local.sRequiredParams.Declare(String)
V.Local.sRetFile.Declare(String)
v.Local.sRet.Declare(String)

'Set attachment filename and path
F.Intrinsic.String.Concat(V.Caller.PluginsDir,"\Invoices\",V.Global.sInvoiceNo,"-",V.Global.sOrderNo,".pdf",V.Local.sAttach)

'Customize project GCG_4262_Invoice_Delivery
f.Intrinsic.String.Concat("select INVOICE_CM_RPTID from Cust_Form_Info WHERE REC = '6' and CUSTOMER = '", v.Global.sCustomer,"'",v.Local.sSQL)
f.ODBC.Connection!conx.ExecuteAndReturn(v.Local.sSQL,v.Local.sInvoiceMemoCredit)
f.Intrinsic.String.Trim(v.Local.sInvoiceMemoCredit,v.Local.sInvoiceMemoCredit)
'=====================================
f.Intrinsic.Control.If(v.Local.sInvoiceMemoCredit,=,"100003")
	v.Local.iRptID.Set(v.Local.sInvoiceMemoCredit)
	v.Local.sReport_Name.Set("OE_INVOICE_AddBuyGroupcopyRev.RPT")	
f.Intrinsic.Control.Else
	v.Local.iRptID.Set(55)
	v.Local.sReport_Name.Set("OE_INVOICE_NoBuyGroupRev.RPT")
f.Intrinsic.Control.EndIf
'================================
'ASK CUSTOMER IF THERE ARE OTHER CASE RATHER THAN 100003 OR 000055'================================

v.Local.sReportID.Set(v.Local.iRptID)
f.Intrinsic.String.LPad(v.Local.sReportID,"0",6,v.Local.sReportID)
F.Intrinsic.String.Build("Delete From PRT_LASER_INVOICE Where Terminal_NO = '{0}'", V.Caller.Terminal, v.Local.sSql)
F.ODBC.Connection!conx.execute(v.Local.ssql)
V.Local.sPN.Set("REPORTID*!*TERMINAL*!*ORDERNO*!*PROGRAM*!*TRACK*!*IVCTYPE*!*PRTSHIP*!*PRTORD*!*IVCTYPE*!*PRTSHIP*!*PRTORD*!*INVOICE")
F.Intrinsic.String.Build("Select Order_NO from Shipment_Lines where Invoice = '{0}' and Record_Type = 'A'", v.Passed.009000, v.Local.sSql)
F.ODBC.Connection!conx.executeAndReturn(v.Local.sSql, v.Local.sRet)

F.Intrinsic.String.Concat(V.passed.009000,"!*!|~|CRYSTAL-OVERRIDE|~|",V.Local.sCallParams)
F.Global.General.CallWrapperSyncBIO(200900,V.Local.sCallParams)

F.Intrinsic.String.Concat(v.Local.sReportID,"*!*",V.caller.Terminal,"*!*",v.Local.sRet,"*!**!*Y*!**!**!**!**!**!**!*",v.Passed.009000,V.Local.sPV)

F.Intrinsic.String.Split(v.Local.sPN,"*!*",v.Local.sParamName)
F.Intrinsic.String.Split(v.Local.sPV,"*!*",v.Local.sParamVal)
F.Global.BI.SaveReport(v.Local.iRptID,1,v.Local.sParamName,v.Local.sParamVal,v.Local.sAttach)

F.Intrinsic.Variable.AddRV("sAttach",V.Local.sAttach)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("ProcessInv_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.ProcessInv.End

Program.Sub.GetRptID.Start
F.Intrinsic.Control.SetErrorHandler("GetRptID_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sQuery.Declare(String)
V.Local.sReportID.Declare(String)

F.Intrinsic.String.concat("SELECT * FROM V_PRT_LASER_INVOICE WHERE INVOICE_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstRptID",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstRptID.EOF,<>,True)
	V.local.sReportID.set(V.odbc.conx!rstRptID.fieldvaltrim!RPTID)
F.Intrinsic.Control.Else
	F.Intrinsic.UI.Msgbox("Unable to find Invoice Data.  Please contact Global Shop.")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstRptID.Close

F.Intrinsic.Variable.AddRV("RptID",V.Local.sReportID)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("GetRptID_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.GetRptID.End

Program.Sub.Check_Table_Data.Start
F.Intrinsic.Control.SetErrorHandler("Check_Table_Data_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sQuery.Declare(String)
V.Local.sError.Declare(String)
V.Local.bExists.Declare(Boolean)

F.Intrinsic.Control.SetErrorHandler("Sub_Err")
F.Intrinsic.Control.ClearErrors

F.Intrinsic.Debug.SetLA("Checking Invoice Table Data.  Rpt ID=",V.Args.RptID," Terminal=",V.Caller.Terminal," Invoice No= ",V.Global.sInvoiceNo," Custom RptID=",V.Args.CustID)

F.Intrinsic.String.Concat("SELECT * FROM PRT_LASER_INVOICE WHERE TERMINAL_NO='",V.Caller.Terminal,"' AND RPTID='",V.Args.RptID,"' AND INVOICE_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
	F.ODBC.conx!rstCheck.Close
	F.Intrinsic.String.Concat("SELECT * FROM PRT_LASER_INVOICE WHERE RPTID='",V.Args.RptID,"' AND INVOICE_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
	F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
	F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
		F.ODBC.conx!rstCheck.Close
		F.Intrinsic.String.Concat("SELECT * FROM PRT_LASER_INVOICE WHERE INVOICE_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
		F.ODBC.Connection!conx.OpenRecordsetRO("rstCheck",V.Local.sQuery)
		F.Intrinsic.Control.If(V.ODBC.conx!rstCheck.EOF,=,True)
'			F.ODBC.conx!rstCheck.Close
			F.Intrinsic.Debug.SetLA("There has been an issue when generating e-Invoice.  Invoice #",V.Global.sInvoiceNo," not found.  Please contact Global Shop.")
			F.Intrinsic.UI.Msgbox(V.Ambient.LastAction)
		F.Intrinsic.Control.Else
			F.Intrinsic.Debug.SetLA("There has been an issue when generating e-Invoice #",V.Global.sInvoiceNo,".  Terminal ID and Report ID is invalid.  Please contact Global Shop.",V.Ambient.NewLine,V.Ambient.NewLine,"Data exists,but terminal and report ID invalid. Terminal in table: ",V.ODBC.conx!rstCheck.FieldValTrim!TERMINAL_NO," rpt ID in table:",V.ODBC.conx!rstCheck.FieldValTrim!RPTID,".Query:",V.Local.sQuery)
			F.Intrinsic.UI.Msgbox(V.Ambient.LastAction)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Else
		F.Intrinsic.Debug.SetLA("There has been an issue when generating e-Invoice #",V.Global.sInvoiceNo,".  Terminal ID is invalid.  Please contact Global Shop.",V.Ambient.LastAction,V.Ambient.LastAction,"Data exists,but terminal invalid. Terminal in table: ",V.ODBC.conx!rstCheck.FieldValTrim!TERMINAL_NO,". Query: ",V.Local.sQuery)
		F.Intrinsic.UI.Msgbox(V.Ambient.LastAction)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Else
	F.Intrinsic.Debug.SetLA("Data exists: ",V.Local.sQuery)
	V.Local.bExists.Set(True)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCheck.Close

F.Intrinsic.Variable.AddRV("bDataExists",V.Local.bExists)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Sub_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Check_Table_Data_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Invoice_Delivery.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.Check_Table_Data.End

Program.Sub.PopDefaultText.Start
'Added 11-24-10 for USPLabs
V.Local.sFile.Declare(String)
V.Local.sText.Declare(String)
V.Local.bExists.Declare(Boolean)
V.Local.sSubject.Declare(String)
V.Local.sBody.Declare(String)
V.Local.sError.Declare(String)

Function.Intrinsic.Control.SetErrorHandler("PopDefaultText_Err")
Function.Intrinsic.Control.ClearErrors

F.Intrinsic.String.Concat(V.Caller.PluginsDir,"\GAB\GAS\eInvoicing_Body_Text.txt",V.Local.sFile)
F.Intrinsic.File.Exists(V.Local.sFile,V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists,=,True)
	F.Intrinsic.File.File2String(V.Local.sFile,V.Local.sBody)
F.Intrinsic.Control.Else
	V.Local.sBody.Set("Attached is your invoice.  Thank you.")
	F.Intrinsic.String.Concat("e-Invoice ",V.Global.sInvoiceNo,V.Local.sSubject)
	F.Intrinsic.File.String2File(V.Local.sFile,V.Local.sBody)
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.CallSub(Getcustinfo)

F.Intrinsic.File.File2String(V.Local.sFile,V.Local.sText)
F.Intrinsic.String.Replace(V.local.sText,"%INVOICE%",V.Global.sInvoiceNo,V.Local.sText)
F.Intrinsic.String.Replace(V.Local.sText,"%CUSTPO%",V.Global.sCustPO,V.Local.sText)
F.Intrinsic.String.Replace(V.Local.sText,"%CUSTOMER%",V.Global.sCustomerName,V.Local.sText)
F.Intrinsic.String.Replace(V.Local.sText,"%COMPANY%",V.Ambient.CompanyName,V.Local.sText)
F.Intrinsic.String.Split(V.Local.sText,"*!*",V.Local.sText)

F.Intrinsic.Control.If(V.Local.sText.UBound,>,0)
	V.Local.sSubject.Set(V.Local.sText(0))
	V.Local.sBody.Set(V.Local.sText(1))
F.Intrinsic.Control.Else
	V.Local.sBody.Set(V.Local.sText)
	F.Intrinsic.String.Concat("e-Invoice ",V.Global.sInvoiceNo,V.Local.sSubject)
F.Intrinsic.Control.EndIf

F.Intrinsic.Variable.AddRV("sBody",V.Local.sBody)
F.Intrinsic.Variable.AddRV("sSubject",V.Local.sSubject)

Function.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.Label("PopDefaultText_Err")
Function.Intrinsic.Control.If(Variable.Ambient.ErrorNumber,"<>",0)
	Function.Intrinsic.String.Concat("Error Occurred ",Variable.Ambient.ErrorNumber," with description ",Variable.Ambient.ErrorDescription,Variable.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf

Program.Sub.PopDefaultText.End

Program.Sub.GetCustInfo.Start
'Added 11/24/10 for USPLabs
V.Local.sQuery.Declare(String)
V.Local.sCustPO.Declare(String)
V.Local.sError.Declare(String)

Function.Intrinsic.Control.SetErrorHandler("GetCustPO_Err")
Function.Intrinsic.Control.ClearErrors

'F.Intrinsic.String.Concat("SELECT CUSTOMER_PO FROM V_ORDER_HEADER WHERE ORDER_NO='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
F.Intrinsic.String.Concat("SELECT CUSTOMER_PO FROM V_ORDER_HIST_HEAD WHERE Invoice='",V.Global.sInvoiceNo,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstCustPO",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCustPO.EOF,<>,True)
	V.Global.sCustPO.Set(V.ODBC.conx!rstCustPO.FieldValTrim!CUSTOMER_PO)
F.Intrinsic.Control.Else
	F.ODBC.conx!rstCustPO.Close
	F.Intrinsic.String.LPad(V.Global.sOrderNo,"0",7,V.Local.sQuery)
	F.Intrinsic.String.Concat("SELECT CUSTOMER_PO FROM V_ORDER_HEADER WHERE ORDER_NO='",V.Local.sQuery,"'",V.Local.sQuery)
	F.ODBC.Connection!conx.OpenRecordsetRO("rstCustPO",V.Local.sQuery)
	F.Intrinsic.Control.If(V.ODBC.conx!rstCustPO.EOF,<>,True)
		V.Global.sCustPO.Set(V.ODBC.conx!rstCustPO.FieldValTrim!CUSTOMER_PO)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCustPO.Close

F.Intrinsic.String.Concat("SELECT NAME_CUSTOMER FROM V_CUSTOMER_MASTER WHERE CUSTOMER='",V.Global.sCustomer,"'",V.Local.sQuery)
F.ODBC.Connection!conx.OpenRecordsetRO("rstCustPO",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.conx!rstCustPO.EOF,<>,True)
	V.Global.sCustomerName.Set(V.ODBC.conx!rstCustPO.FieldValTrim!NAME_CUSTOMER)
F.Intrinsic.Control.EndIf
F.ODBC.conx!rstCustPO.Close

Function.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.Label("GetCustPO_Err")
Function.Intrinsic.Control.If(Variable.Ambient.ErrorNumber,"<>",0)
	Function.Intrinsic.String.Concat("Error Occurred ",Variable.Ambient.ErrorNumber," with description ",Variable.Ambient.ErrorDescription,Variable.Local.sError)
	F.Intrinsic.ui.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
Function.Intrinsic.Control.EndIf
Program.Sub.GetCustInfo.End
Program.Sub.Form_UnLoad.Start
Program.Sub.Form_UnLoad.End
Program.Sub.F_EmailResult_UnLoad.Start
gui.F_EmailResult..Visible(False)
Program.Sub.F_EmailResult_UnLoad.End





Program.Sub.Comments.Start
${$0$}$GAB_4227_Overdue_Invoices$}$A. Williams$}$8/30/2016 6:43:30 PM$}$False
${$3$}$0$}$$}$0$}$-1$}$$}$12:00:00 AM$}$
Program.Sub.Comments.End
